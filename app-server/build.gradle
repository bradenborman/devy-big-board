plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.2'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'borman'
version='0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://repo.spring.io/snapshot' }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter'

	//SQL
	implementation 'mysql:mysql-connector-java:8.0.33'

	// Export dependencies
	implementation 'com.opencsv:opencsv:5.9'
	implementation 'org.apache.pdfbox:pdfbox:3.0.1'
	// Jackson is already included via spring-boot-starter-web

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testRuntimeOnly 'com.h2database:h2'
}

tasks.named('test') {
	useJUnitPlatform()
}

task copyClient(dependsOn: ":app-client:clientBuild") {

	doFirst {
		delete "src/main/resources/static"
		delete "src/main/resources/templates"
	}
	doLast {
		copy {
			from new File(project(":app-client").getProjectDir(), "build")
			into "src/main/resources/static"
		}
		copy {
			from new File(project(":app-client").getProjectDir(), "build/index.html")
			into "src/main/resources/templates"
		}
	}
}

jar {
	enabled = false
}

task finalizeBuild {
	dependsOn 'build'
	doLast {
		def buildLibs = file("${buildDir}/libs")
		def oldJar = file("${buildLibs}/app-server-${version}.jar")
		def newJar = file("${buildLibs}/devy-big-board.jar")
		if (oldJar.exists())
			oldJar.renameTo(newJar)
	}
}


processResources.dependsOn(copyClient)
clean.dependsOn(":app-client:clientClean")
build.finalizedBy(finalizeBuild)

task stage(dependsOn: ['build', 'clean'])
build.mustRunAfter clean